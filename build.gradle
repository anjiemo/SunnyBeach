// AndroidProject 版本：v13.0
// 导入配置文件
apply from : 'configs.gradle'

buildscript {
    // 导入通用的 Maven 库配置
    apply from : 'maven.gradle'
    ext.kotlin_version = "1.6.0"
    ext.agcp_version  = "1.6.0.300"
    repositories {
        // 阿里云云效仓库（Gradle 插件）：https://maven.aliyun.com/mvn/guide
        maven { url 'https://maven.aliyun.com/repository/gradle-plugin' }
        addCommonMaven(repositories)
    }

    // Studio 插件推荐：https://github.com/getActivity/StudioPlugins
    dependencies {
        // Gradle 插件版本说明：https://developer.android.google.cn/studio/releases/gradle-plugin.html#updating-plugin
        classpath 'com.android.tools.build:gradle:7.1.3'
        // AOP 配置插件：https://github.com/HujiangTechnology/gradle_plugin_android_aspectjx
        classpath 'com.hujiang.aspectjx:gradle-android-plugin-aspectjx:2.0.10'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        // 增加agcp插件配置
        classpath "com.huawei.agconnect:agcp:$agcp_version"
        classpath "com.google.dagger:hilt-android-gradle-plugin:2.40.5"
    }
}

allprojects {
    repositories {
        // 友盟远程仓库：https://info.umeng.com/detail?id=443&cateId=1
        maven { url 'https://repo1.maven.org/maven2' }
        addCommonMaven(repositories)
    }

    tasks.withType(JavaCompile) {
        // 设置全局编码
        options.encoding = 'UTF-8'
    }
    tasks.withType(Javadoc) {
        // 设置文档编码
        options {
            encoding 'UTF-8'
            charSet 'UTF-8'
            links 'http://docs.oracle.com/javase/8/docs/api'
        }
    }

    // 将构建文件统一输出到项目根目录下的 build 文件夹
    setBuildDir(new File(rootDir, "build/${path.replaceAll(':', '/')}"))
}

task clean(type: Delete) {
    delete rootProject.buildDir
}